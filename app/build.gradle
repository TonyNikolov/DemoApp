apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def marvelApiKeyPropertiesFile = rootProject.file("marvelApiKey.properties")
def marvelApiKeyProperties = new Properties()
marvelApiKeyProperties.load(new FileInputStream(marvelApiKeyPropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    flavorDimensions "version"

    defaultConfig {
        applicationId "com.dnt.demoapp"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    productFlavors {
        dev {
            applicationIdSuffix '.dev'
            buildConfigField 'String', 'MARVEL_API_ENDPOINT', '"https://gateway.marvel.com/"'
            buildConfigField 'String', 'MARVEL_PUBLIC_API_KEY', marvelApiKeyProperties['marvelPublicApiKey']
            buildConfigField 'String', 'MARVEL_PRIVATE_API_KEY', marvelApiKeyProperties['marvelPrivateApiKey']
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'

    // Base UI
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"

    // Lifecycle Scope
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    // Dagger2
    implementation "com.google.dagger:dagger:2.28"
    kapt "com.google.dagger:dagger-compiler:2.28"

    // Room
    implementation "android.arch.persistence.room:runtime:2.1.0"
    kapt "android.arch.persistence.room:compiler:2.1.0"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.6.2"
    implementation "com.squareup.retrofit2:retrofit-mock:2.6.2"
    implementation "com.squareup.retrofit2:converter-gson:2.6.2"
    implementation "com.squareup.okhttp3:okhttp:3.14.1"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.1"

    // Parsing
    implementation "com.google.code.gson:gson:2.8.6"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Testing
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}